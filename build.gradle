plugins {
	id "net.neoforged.gradle" version "[6.0.13,7)"
	id "org.parchmentmc.librarian.forgegradle" version "[1,)"
	id "org.spongepowered.mixin" version "0.7-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
}

repositories {
	jcenter()
	flatDir {
		dirs "libs"
	}
	maven {
		url = "https://repo.spongepowered.org/maven/"
	}
	maven {
		url = "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup "slimeknights.mantle"
			includeGroup "slimeknights.tconstruct"
		}
	}
	maven {
		url = "https://maven.blamejared.com/"
		content {
			includeGroup "mezz.jei"
			includeGroup "blusunrize.immersiveengineering"
			includeGroup "com.blamejared.crafttweaker"
			includeGroup "net.darkhax.openloader"
			includeGroup "vazkii.patchouli"
		}
	}
	maven {
		url = "https://maven.theillusivec4.top/"
		content {
			includeGroup "top.theillusivec4.curios"
		}
	}
	maven {
		url = "https://maven.architectury.dev/"
		content {
			includeGroup "dev.architectury"
		}
	}
	maven {
		url = "https://maven.saps.dev/releases/"
		content {
			includeGroup "dev.ftb.mods"
			includeGroup "dev.latvian.mods"
		}
	}
	maven {
		url = "https://maven.tterrag.com/"
		content {
			includeGroup "com.simibubi.create"
		}
	}
	maven {
		url = "https://maven.rover656.dev/releases/"
		content {
			includeGroup "com.enderio"
		}
	}
	maven {
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
		content {
			includeGroup "software.bernie.geckolib"
		}
	}
	maven {
		url = "https://maven.covers1624.net/"
		content {
			includeGroup "com.teamcofh"
		}
	}
	maven {
		url = "https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1/"
		content {
			includeGroup "com.kotori316"
		}
	}
	maven {
		url = "https://jenkins.bdew.net/maven/"
		content {
			includeGroup "net.bdew"
		}
	}
	maven {
		url = "https://dogforce-games.com/maven/"
		content {
			includeGroup "dev.gigaherz.jsonthings"
		}
	}
	maven {
		url = "https://cursemaven.com/"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url = "https://modmaven.dev/"
	}
}

sourceSets {
	main
}

def mcVersion = "1.20.1"
def modName = "JAOPCA"
def modId = "jaopca"
def specVersion = "3"
def vendor = "thelm"

group = "thelm.jaopca"
version = "4.4.5.8"

archivesBaseName = "${modName}-${mcVersion}"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

minecraft {
	mappings channel: "parchment", version: "2023.07.30-1.20.1"
	generateRunFolders = true
	runs {
		configureEach {
			workingDirectory project.file("run")
			property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
			property "forge.logging.console.level", "debug"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			mods {
				"${modId}" {
					source sourceSets.main
				}
			}
		}
		client {
			jvmArg "-Xmx7G"
		}
		server {
			jvmArg "-Xmx3G"
		}
	}
}

dependencies {
	minecraft "net.neoforged:forge:1.20.1-47.1.84"
	annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

	implementation fg.deobf("mezz.jei:jei-1.20.1-forge:15.2.0.27")
	implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-forge-1.20.1:14.0.27") {
		transitive = false
	}
	implementation fg.deobf("top.theillusivec4.curios:curios-forge:5.4.6+1.20.1")
	implementation fg.deobf("software.bernie.geckolib:geckolib-neoforge-1.20.1:4.3.1")
	implementation fg.deobf("vazkii.patchouli:Patchouli:1.20.1-84-FORGE")
	//implementation fg.deobf("slimeknights.mantle:Mantle:1.20.1-")
	implementation fg.deobf("com.teamcofh:cofh_core:1.20.1-11.0.0.51")
	implementation fg.deobf("com.teamcofh:thermal_core:1.20.1-11.0.1.17")
	implementation fg.deobf("dev.latvian.mods:rhino-forge:2001.2.2-build.18")
	implementation fg.deobf("dev.architectury:architectury-forge:9.1.12")
	//implementation fg.deobf("dev.ftb.mods:ftb-library-forge:2001.1.4") {
	//	transitive = false
	//}
	implementation("com.kotori316:scalablecatsforce:3.3.0-build-2:with-library") {
		transitive = false
	}
	implementation fg.deobf("dev.latvian.mods:kubejs-forge:2001.6.4-build.114") {
		transitive = false
	}
	implementation fg.deobf("net.darkhax.openloader:OpenLoader-Forge-1.20.1:19.0.3")
	implementation fg.deobf("dev.gigaherz.jsonthings:JsonThings-1.20.1:0.9.1")
	implementation fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:1.20.1-10.0.0-169.40")
	implementation fg.deobf("com.simibubi.create:create-1.20.1:0.5.1.f-26:all") {
		transitive = false
	}
	//implementation fg.deobf("slimeknights.tconstruct:TConstruct:1.20.1-")
	implementation fg.deobf("mekanism:Mekanism:1.20.1-10.4.5.19")
	implementation fg.deobf("mekanism:Mekanism:1.20.1-10.4.5.19:generators")
	//implementation fg.deobf("dev.ftb.mods:ftb-industrial-contraptions:2001.") {
	//	transitive = false
	//}
	implementation fg.deobf("com.teamcofh:thermal_foundation:1.20.1-11.0.1.63:all")
	implementation fg.deobf("com.teamcofh:thermal_expansion:1.20.1-11.0.0.27")
	implementation fg.deobf("com.teamcofh:thermal_integration:1.20.1-11.0.0.23")
	//implementation fg.deobf("redsnakegames.mcenderdragon:futurepack:1.20.1-")
	implementation fg.deobf("com.enderio:EnderIO:1.20.1-6.0.22-alpha")

	//CurseMaven
	implementation fg.deobf("curse.maven:ato-405593:4844097")
	implementation fg.deobf("curse.maven:allthemodium-364466:4892369")
	implementation fg.deobf("curse.maven:electrodynamics-316867:4903241")
	implementation fg.deobf("curse.maven:voluminous-energy-416704:4627288")
	implementation fg.deobf("curse.maven:cyclops-core-232758:4905983")
	implementation fg.deobf("curse.maven:common-capabilities-247007:4620480")
	implementation fg.deobf("curse.maven:integrated-dynamics-236307:4925678")
	implementation fg.deobf("curse.maven:essentials-293752:4640300")
	//implementation fg.deobf("curse.maven:crossroads-mc-250231:")
	//implementation fg.deobf("curse.maven:assemblylinemachines-388282:")
	implementation fg.deobf("curse.maven:smartbrainlib-661293:4743400")
	implementation fg.deobf("curse.maven:modonomicon-538392:4976313")
	implementation fg.deobf("curse.maven:occultism-361026:4973420")
	implementation fg.deobf("curse.maven:flib-661261:4724762")
	implementation fg.deobf("curse.maven:cyclic-239286:4976541")
    implementation fg.deobf("curse.maven:blood-magic-224791:4779711")
	//implementation fg.deobf("curse.maven:industrial-reborn-358877:")
	//implementation fg.deobf("curse.maven:ic2-classic-242942:")
	implementation fg.deobf("curse.maven:bdlib-70496:4670650")
	implementation fg.deobf("curse.maven:factorium-583423:4670915")
	implementation fg.deobf("curse.maven:energized-power-782147:4934066")
	implementation fg.deobf("curse.maven:embers-rekindled-300777:4897329")

	//Performance
	implementation fg.deobf("curse.maven:yeetusexperimentus-635427:4680832")
	implementation fg.deobf("curse.maven:modernfix-790626:4980929")
	implementation fg.deobf("curse.maven:ferritecore-429235:4810975")
}

def manifestAttributes = [
	"Specification-Title": modName,
	"Specification-Vendor": vendor,
	"Specification-Version": specVersion,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": version,
	"Implementation-Vendor": vendor,
	"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
	"MixinConnector": "thelm.jaopca.mixins.JAOPCAMixinConnector"
]

jar {
	manifest.attributes(manifestAttributes)
	finalizedBy("reobfJar")
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

mixin {
	add sourceSets.main, "jaopca.refmap.json"
}
